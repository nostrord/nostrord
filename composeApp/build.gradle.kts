import org.jetbrains.compose.desktop.application.dsl.TargetFormat
import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.composeHotReload)
}

kotlin {
    // Suppress expect/actual classes beta warnings
    compilerOptions {
        freeCompilerArgs.add("-Xexpect-actual-classes")
    }

    androidTarget {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
        }
    }
    
    listOf(
        iosArm64(),
        iosSimulatorArm64()
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "ComposeApp"
            isStatic = true
        }
    }
    
    jvm()
    
    js {
        browser {
            // Configure webpack for production builds
            webpackTask {
                mainOutputFileName = "composeApp.js"
            }
        }
        binaries.executable()
    }

    @OptIn(ExperimentalWasmDsl::class)
    wasmJs {
        browser {
            // Configure webpack for production builds
            webpackTask {
                mainOutputFileName = "composeApp.js"
            }
        }
        binaries.executable()
    }
    
    sourceSets {
        androidMain.dependencies {
            implementation(compose.preview)
            implementation(libs.androidx.activity.compose)
            implementation("io.ktor:ktor-client-okhttp:3.0.0")
            implementation("fr.acinq.secp256k1:secp256k1-kmp-jni-android:0.14.0")
            implementation("androidx.security:security-crypto:1.1.0-alpha06")
            implementation("org.bouncycastle:bcprov-jdk18on:1.78.1")
        }
        
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material3)
            implementation(compose.ui)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)
            implementation(libs.androidx.lifecycle.viewmodelCompose)
            implementation(libs.androidx.lifecycle.runtimeCompose)
            implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1")
            implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3")
            implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.6.1")
            
            implementation("io.ktor:ktor-client-core:3.0.0")
            implementation("io.ktor:ktor-client-websockets:3.0.0")
            implementation("io.ktor:ktor-client-content-negotiation:3.0.0")
            implementation("io.ktor:ktor-serialization-kotlinx-json:3.0.0")
            implementation("io.coil-kt.coil3:coil-compose:3.0.4")
            implementation("io.coil-kt.coil3:coil-network-ktor3:3.0.4")
            implementation(compose.materialIconsExtended)
        }
        
        commonTest.dependencies {
            implementation(libs.kotlin.test)
        }
        
        jvmMain.dependencies {
            implementation(compose.desktop.currentOs)
            implementation(libs.kotlinx.coroutinesSwing)
            implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.6.1")
            implementation("media.kamel:kamel-image:0.9.5")
            implementation("io.ktor:ktor-client-cio:3.0.0")
            implementation("fr.acinq.secp256k1:secp256k1-kmp:0.14.0")
            implementation("fr.acinq.secp256k1:secp256k1-kmp-jni-jvm:0.14.0")
            implementation("org.bouncycastle:bcprov-jdk18on:1.78.1")
            implementation("org.slf4j:slf4j-nop:2.0.9")
        }
        
        jsMain.dependencies {
            implementation("io.ktor:ktor-client-js:3.0.0")
            implementation("media.kamel:kamel-image:0.9.5")
            // Compression webpack plugin for production builds
            implementation(devNpm("compression-webpack-plugin", "11.1.0"))
        }

        val wasmJsMain by getting {
            dependencies {
                // Ktor JS engine for HTTP requests (required for Coil image loading)
                implementation("io.ktor:ktor-client-js:3.0.0")
                // Compression webpack plugin for production builds
                implementation(devNpm("compression-webpack-plugin", "11.1.0"))
            }
        }
    }
}

android {
    namespace = "org.nostr.nostrord"
    compileSdk = libs.versions.android.compileSdk.get().toInt()

    defaultConfig {
        applicationId = "org.nostr.nostrord"
        minSdk = libs.versions.android.minSdk.get().toInt()
        targetSdk = libs.versions.android.targetSdk.get().toInt()
        versionCode = 1
        versionName = "1.0"
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
            excludes += "META-INF/versions/9/OSGI-INF/MANIFEST.MF"
            excludes += "META-INF/DEPENDENCIES"
        }
    }
    buildTypes {
        getByName("release") {
            isMinifyEnabled = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

dependencies {
    debugImplementation(compose.uiTooling)
}

compose.desktop {
    application {
        mainClass = "org.nostr.nostrord.MainKt"

        nativeDistributions {
            targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
            packageName = "org.nostr.nostrord"
            packageVersion = "1.0.0"
        }
    }
}

// Copy pre-compressed files (.gz, .br) to distribution folder for wasmJs
tasks.register<Copy>("copyWasmJsCompressedFiles") {
    dependsOn("wasmJsBrowserProductionWebpack")
    from(layout.buildDirectory.dir("kotlin-webpack/wasmJs/productionExecutable")) {
        include("**/*.gz", "**/*.br")
    }
    into(layout.buildDirectory.dir("dist/wasmJs/productionExecutable"))
}

// Copy pre-compressed files for js target
tasks.register<Copy>("copyJsCompressedFiles") {
    dependsOn("jsBrowserProductionWebpack")
    from(layout.buildDirectory.dir("kotlin-webpack/js/productionExecutable")) {
        include("**/*.gz", "**/*.br")
    }
    into(layout.buildDirectory.dir("dist/js/productionExecutable"))
}

// Make distribution tasks depend on copy tasks
tasks.matching { it.name == "wasmJsBrowserDistribution" }.configureEach {
    finalizedBy("copyWasmJsCompressedFiles")
}

tasks.matching { it.name == "jsBrowserDistribution" }.configureEach {
    finalizedBy("copyJsCompressedFiles")
}
